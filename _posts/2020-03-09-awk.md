---
layout: post
title: AWK
date: 2020-03-09
categories: [man]
tags: [programming language, utility]
author: "Max Kossek"
description: Reference for the AWK programming language.
sitemap:
    lastmod: 2020-05-11
---

NAME
----

AWK - Pattern-directed scanning and processing language.


DESCRIPTION
-----------

AWK scans a file and splits each input line into fields. The basic structure of an AWK program consists of a pattern followed by an action: pattern { action }. Each input line is tested against each of the patterns. If a pattern has no action, then the line is printed. If there is an action with no pattern, then the action is executed for each input line. Simple line processing can be accomplished directly from the command line.
```
awk '$2 > 0 { print $1 * $2 }' file
```

To create and run an AWK script from a file, create a file with an ".awk" extension containing a shebang line. Alternatively, AWK commands can be embedded within a shell script.
```
#!/usr/bin/awk -f
BEGIN { print "Hello World" }

#!/usr/sh
awk '
BEGIN { print "Hello World" }
'
```

The BEGIN{} statement is executed once before the first input has been read, while the END{} statement is executed once all the input has been read. The BEGIN{} section is often used for variable initialization, to change the way lines are split, or to print a header. The next statement causes AWK to fetch the next input line. The exit statement terminates a program in an END action. In other sections exit signals an end of input, causing the END action to execute.
```
BEGIN {
	printf("Start:\n");
	FS = ",";
}
$1 > 0 {
	x += $1;
}
{
	if (NF < 3)
		exit 1;
	y += $2;
}
END {
	printf("x = %d\ny = %d\n", x, y);
	printf("Exit\n");
}
```

String-matching patterns use regular expressions in the form /PATTERN/ to match the input line. Range patterns have the form: "pattern1, pattern2". Actions are performed on each line starting from the first pattern until the second pattern evaluates to true. Compound expression are separated by the boolean operators "&&", "||", or "!".
```
/REGEXPATTERN/ {
	...
}
$1 > 0 && !(NF < 4 || /password/) {
	...
}
NR == 10, NR == 20 {
	...
}
```


OPTIONS
-------

-F separator - Set the character to be used as the field separator (e.g. -F,).


OUTPUT
------

Both the print and printf statements can be used to generate output. The print statement prints unformatted strings and separates each output according to the output field separator variable (OFS). The printf function provides more control over formatting and does not separate output by the OFS variable. Both print and printf can have optional parentheses "()" enclosing the expression. Semicolons ";" at the end of a line can be used to separate several statements on the same line.
```
print expr1, expr2, ..., exprn
print $1, $2
print "hello world"

printf format, expr1, expr2, ..., exprn
printf "hello %s\n", "world"
printf("hello %d\n", "2");
```

Output from print and printf can be redirected to a file using the ">" operator. ">>" appends the output to the original contents of a file. Parentheses should be used to remove ambiguity between a comparison and a redirection.
```
printf("Write to file\n") > "/file";

# Redirection
{ print $1, $2 > $3 }
# Comparison
{ print $1, ($2 > $3) }
```

Separating two variables by a comma makes AWK print the fields separated by the OFS variable. Separating two values by a space concatenates the strings. Printing NF outputs the number of columns in the current line, while printing $NF outputs the value stored in the last field.
```
print $1 $2;
print $1, $2;

print NF;
print $NF;
```

AWK does not evaluate variables within strings.
```
$ awk '{print $0}'
> test
test
$ awk '{print "$0"}'
> test
$0
```


INPUT
-----

STDIN becomes the input to an AWK program if no input file is specified. The program { print; } simply prints out all lines received through STDIN. Input from STDIN can also be read using the getline function. Once the end of input is reached, getline returns -1.
```
while ((input = getline) > 0)
	print $0
```


VARIABLES
---------

Numbers and strings are the only data types in AWK. AWK infers a variables type from context. Strings are enclosed in double quotes. Numbers are stored in floating point representation. User variables are not declared. Variables are initialized to the string value "", and the numeric value 0.
```
{
	n += 5;
	s = s $1 " ";
	print n, s;
}
```

Built-in variables used for line processing:

$0 - Input line (all field values).

$1, $2, ... - Field values.

ARGC - Argument count.

ARGV - Argument vector.

ENVIRON - Array of environment values.

FILENAME - Current file being read.

FS - Field separator. The default is " ". Set it with FS = ",".

NF - Number of fields in the current line (columns). There is a limit of 99 fields per line.

NR - Number of records read (rows).

OFS - Output field separator. The default is " ".

ORS - Output record separator. The default is "\n".

RS - Record separator. The default is "\n".


OPERATORS
---------

Basic arithmetic operators: +, -, *, /, %, ^, ++. There are also several built-in arithmetic functions: atan2(y,x), cos(x), exp(x), int(x), log(x), rand(), sin(x), sqrt(x), srand(x).
```
x = y + 1;
x = 5 ^ 2;
++x;
x = sqrt(y);
```

Incrementing and decrementing operators: ++, --. x++ evaluates x's value and then increments it. ++x increments the variables value, and then evaluates it.
```
x = --y;
```

Assignment operators: =, +=, -=, *=, /=, %=, ^=.
```
FS = OFS = "\t";
x += 1;
```

Comparison operators: ==, !=, <, <=, >, >=, ~ (matched by), !~ (not matched by). The value of a true comparison is 1, and 0 if false.
```
$1 > 0 { ... }
```

Logical operators: &&, ||, !. Expressions are evaluated from left to right, and evaluation ceases if the value of an expression can be determined early. Operands with a non-zero or non-null value are treated as true.
```
if (!x) { ... }
$1 > 0 || $2 > 10
```


STRINGS
-------

Any number of strings can be concatenated by simply separating them with a whitespace.
```
x = "a" "bc";
x = x "de"
```

The index() function searches for a character's index inside a string. The length() function returns the length of a string. The split() function separates a string into an array based on an optional separator and returns the number of elements. The substr() function returns the substring starting at a position. It takes in an optional length parameter that limits the number of characters to be returned.
```
index($0, "\t");
index($0, "word");
length($0);
split($0, array);
split("2000-01-31", array, "-");
substr($0, 5);
substr($0, 3, 2);
```

The match() function tests whether a string contains a substring matched by a regular expression. It returns the starting index or 0, and sets the built-in variables RSTART and RLENGTH. The gsub(r, s) function substitutes the string s for r in $0 and returns the number of substitutions made. The sub(r,s) function substitutes s for the leftmost longest substring of $0 matched by r, and returns the number of substitutions made.
```
gsub(/USA/, "United States");
```


ARRAYS
------

All AWK arrays are associative and one-dimensional. Associative arrays use strings as array indexes. Array indexes can be generated in a number of ways, such as by concatenating two fields. Like variables, array elements are initialized to 0 and "".
```
BEGIN {
	my_array[""] = 0;
}
{
	my_array[$0] += $5;
	my_array["num"]++;
	my_array[$1 $2]++;
}
```

Multi-dimensional arrays can be simulated by concatenating subscripts. The example below creates a one-dimensional array, but with subscripts "1,1", "1,2" and so on.
```
for (i = 1; i <= 10; i++)
	for (j = 1; j <= 10; j++)
		arr[i,j] = 0
```

The "in" expression tests if a subscript exists in an array. An array element can be deleted using the delete statement.
```
if ("hello" in arr) ...

for (i in arr)
	delete arr[i]
```


FUNCTIONS
---------

Awk supports user-defined functions. Ordinary variables are passed by value, while arrays are passed by reference. Within a function definition, parameters are local variables. The body of a function can contain an optional return statement that returns a value to the caller. Functions can be called recursively.
```
function max(m, n) {
	return m > n ? m : n;
}

{
	print max($1, max($2, $3));
}
```


CONDITIONALS
------------

Conditional expressions have the form: "expr1 ? expr2 : expr3". If expr1 evaluates to true, then the value of the conditional expression is expr2; else the value is expr3.
```
$1 != 0 ? 1/$1 : "$1 is zero, line " NR;
```

If-else statements have the same syntax as the C programming language.
```
if (i < 0) {
	x = 1;
	$2 = "";
}
else if (i > 0)
	x = -1;
else
	x = 0;
```


LOOPS
-----

The while, do and for loop constructs are adopted from C. A break statement causes an early exit from the immediately enclosing while, do, or for structure. The "continue" keyword causes a loop to skip to the next iteration.
```
while (i < 0)
	i++;

do {
	i++;
}
while (i < 0);

for (i = 0; i < 100; i++)
	y += i;

for (;;)
	break;
```


EXAMPLES
--------

Only print the lines whose second field value is greater than 0.
```
awk '$2 > 0'
```

Print every line longer than 80 characters.
```
awk 'length($0) > 80'
```

Print all lines beginning with a letter lexically greater or equal to "M".
```
awk '$0 >= "M"'
```

Print all lines that begin with a number.
```
awk '/^[0-9]/'
```

Print all lines in which the first field contains the string "abc".
```
awk '$1 ~ /abc/'
```
