---
layout: post
title: Haskell
date: 2020-05-28
categories: [man]
tags: [language, functional]
author: "Max Kossek"
description: Personal reference for the Haskell functional programming language.
sitemap:
    lastmod: 2020-05-28
---

NAME
----

Haskell -- Purely functional programming language.



DESCRIPTION
-----------

To open an interactive prompt use `ghci`: `$ ghci`. Use the `:load` or `:l` command to load a Haskell source file into the interactive session. The `:reload` command reloads a source file. The `import` command loads a module into the interactive session. Type `:quit` or `:q` to exit the interactive GHCi.

Single-line comments begin with "--" and span until the end of the line. Block comments have the form: "{- comment -}".

Haskell source code files have a ".hs" extension. The `main` function inside the `Main` module defines the entry point of a program. Haskell names are case sensitive: module names are capitalized and definitions are uncapitalised. To compile a program using the Glasgow Haskell Compiler: `$ ghc filename.hs`.
```
module Main (main) where

main :: IO ()
main = putStrLn "Hello, World!"
```



INPUT / OUTPUT
--------------

The `putStr` function takes in a string argument and prints it to the screen. The `putStrLn` function is the same as `putStr`, except that it appends a newline character.
```
> putStr "Hello, World!"
Hello, World!>

> putStrLn "Hello, World!"
Hello, World!
>
```

The `getLine` function reads a line from the standard input. The backwards arrow operator ("<-") assigns the returned value to a variable.
```
main = do
  putStrLn "Please enter your name:"
  name <- getLine
  putStrLn ("Hello, " ++ name ++ ", how are you?")
```

The "do" keyword links some number of actions together. All actions in a "do" block must be aligned with the first non-whitespace character after the "do" keyword. The "let" keyword is used to create a local binding. A "let" binding inside an action does not require the "in" keyword.
```
rev =
 do inputStr <- getLine
    let revInput = reverse inputStr
    putStrLn ("Reversed: " ++ revInput)
```

The `read` function casts strings to another type. The `show` function cast a type into a string.
```
> :t read
read :: Read a => String -> a
> read "3" :: Int
3
> read "3.14" :: Double
3.14
> read "True" :: Bool
True

> :t show
show :: Show a => a -> String
> show 3.14
"3.14"
> show False
"False"
```

The `getArgs` function in `System.Environment` reads the command line arguments to a program.
```
import System.Environment

main = do
  args <- getArgs
  putStrLn ("Arg1: " ++ args !! 0 ++ "\nArg2: " ++ args !! 1)
```



DATA TYPES
----------

In Haskell, all operations, functions, and variables have a defined or inferred type. Haskell's strict type system ensures that illegal operations can be caught early. As a result, Haskell has far fewer run-time errors than other languages.

The symbol "::" indicates "is of type". The type-of symbol can be used declare a function's signature. Type signatures should go above the function definition. The Haskell compiler can perform type inference on most operations/functions that you did not write a signature for. However, it is still generally useful to include type signatures for documentation and debugging purposes.
```
xor :: Bool -> Bool -> Bool
xor p q = (p || q) && not (p && q)

myCons :: a -> [a] -> [a]
myCons thing list = thing : list

myNumCons :: Num a => a -> [a] -> [a]
myNumCons thing list = thing : list

uppercase, lowercase :: String -> String
uppercase = map toUpper
lowercase = map toLower
```

The function signature `f :: a -> a` describes a function that takes in any type and returns a value of the same type. The signature `f :: a -> b` indicates that the function returns a different type than its argument.
```
> :t head
head :: [a] -> a

> :t snd
fst :: (a, b) -> b
```

The `:type` or `:t` interactive command evaluates the type of an expression.
```
> :t 1
1 :: Num p => p

> :t 3.0
3.0 :: Fractional p => p

> :t 'h'
'h' :: Char

> :t "hello"
"hello" :: [Char]

> :t (3 < 5)
(3 < 5) :: Bool

> :t not
not :: Bool -> Bool

> f x y = not x && y
> :t f
f :: Bool -> Bool -> Bool

> :t or
or :: Foldable t => t Bool -> Bool

> isL c = c == 'l'
> :t isL
isL :: Char -> Bool
```


Standard Haskell data types:

`Applicative`
: Data types that support applying functions within the functor.

`Bool`
: Boolean type. Either `True` or `False`.

`Char`
: Character such as 'a' or '?'. A string such as `"hello"` is of type `[Char]` (a list of characters).

`Double`
: Double-precision floating point.

`Either a b`
: Either type `a` or type `b`. Defined as `data Either a b = Left a | Right b`.

`Enum`
: Sequentially ordered types.

`Eq`
: Typeclass consisting of all the types that can be compared for equality (all primitive types except functions and IO).

`Float`
: Single-precision floating point.

`Floating`
: Floating point numbers (`Float` and `Double`).

`Foldable`
: Typeclass consisting of all the types that can be folded to a list.

`Fractional`
: Subclass of the `Num` typeclass.

`Functor`
: Types which can be mapped over.

`Int`
: Integers of fixed-size, such as 1, 50.

`Integer`
: Integer numbers with arbitrarily large values.

`Integral`
: Whole numbers (`Int` and `Integer`).

`IO`
: Actions involving input and output.

`Maybe a`
: Optional value. A `Maybe` value either has a value of type `a` (`Just a`) or is empty (`Nothing`).

`Monad`
: Monad, supporting a bind and return operation.

`Num`
: Typeclass consisting of the `Int`, `Integer`, `Float`, and `Double` data types. Numbers such as 1, 3.0.

`Ord`
: Types that have an ordering. Almost all basic types are members of `Ord`.

`Show`
: Types that can be represented as strings.

`String`
: Synonym for `[Char]` data type. Strings are simply a list of characters.


A typeclass is a group of types. Typeclasses allow you to define functions for a set of data types without having to write out separate definitions for each type. For example, the "+" operator works on any value of `Num` type.

Everything before the arrow operator ("=>") is a class constraint in a type declaration. The `Num` typeclass consists of all the types that are numbers. The `Eq` typeclass consists of all the values that can be checked for equality. The `Foldable` typeclass consists of all data type values that can be folded to a list. 
```
(+) :: Num a => a -> a -> a

(==) :: Eq a => a -> a -> Bool

length :: Foldable t => t a -> Int
```

Haskell supports type synonyms / aliases: alternative names for types. The compiler treats two types that are aliases as identical. For example, the `String` type is equivalent to `[Char]`.
```
type Name = String

name : Name
name = "John"
```

Haskell also allows users to define their own types (using a `data` declaration), assign alternative names to existing types (using the `type` declaration), and define new types as equivalent to existing ones (using the `newtype` declaration). Type names and constructor function names must start with a capital letter.
```
data Date = Date Int Int Int

feb :: Date
feb = Date -1 2 -1

type dateList = [Date]

getMonth :: Date -> Int
getMonth (Date _ m _) = m

showDate :: Date -> String
showDate (Date y m d) = show y ++ "-" ++ show m ++ "-" ++ show d
```

The syntax of a `data` declaration is `data DataType = ConstructorName [types] | ConstructorName2 [types] | ...`.
```
data Complex a = Num a => Complex a a

data Complex a = Num a => Complex { real :: a, img :: a}
c = Complex 1.0 2.0
z = Complex { real = 3, img = 4 }
real c -> 1.0
img z -> 4
```

An enumeration is a `data` declaration where none of the constructors take in any arguments.
```
data Month = January | February | March
data Bool = Richtig | Falsch
```

A parameterized type consists of one or more type parameters. The `Maybe` type is commonly used to handle failures. The `Either` type specifies that a function can return one of a number of types.
```
data Maybe a = Nothing | Just a

data Either a b = Left a | Right a

respond :: Int -> Either String Int
respond x
  | x < 0     = Right 1
  | otherwise = Left "Error"
```

The "instance" keyword makes a data type an instance of an existing data type.
```
data IntStr = IntStr {i :: Integer, s :: String}

instance Eq IntStr where
  (IntStr i1 s1) == (IntStr i2 s2) = (i1 == i2) && (s1 == s2)

IntStr 3 "a" == IntStr 5 "b"
```

The "deriving" keyword automatically generates instance definitions for a data type. It can be used on the following built-in classes: `Eq`, `Ord`, `Enum`, `Bounded`, `Show`, and `Read`. Compared to "instance", using "deriving" is faster but also gives you less control to write custom functionality.
```
data IntStr = IntStr {i :: Integer, s :: String}
  deriving (Eq, Ord, Show)
```



POLYMORPHIC TYPES
-----------------

Haskell supports polymorphic types. This means that a function can consume and return values of different data types. The type variable (`a` in the example below), has to be lowercase. The `Maybe` type can hold a variable of any type or `Nothing`.
```
data Maybe a = Nothing | Just a
```

The `fromIntegral` function takes in an argument of `Integral` type and makes it a value of the polymorphic type `Num`.
```
fromIntegral :: (Integral a, Num b) => a -> b
```

To declare a polymorphic type, write a `data` declaration with a lowercase type variable. The type variable is then used as a parameter during variable creation.
```
data List t = E | C t (List t)

lst1 :: List Int
lst1 = C 1 (C 5 (C -1 E))

lst2 :: List Char
lst2 = C 'x' (C 'y' (C 'z' E))

lst3 :: List Bool
lst3 = C True (C False E)
```



LISTS
-----

Lists are built by placing comma-separated values inside square brackets "[]". All the elements of a list must be of the same type.
```
> ['a', 'b']
"ab"

> [True, False]
[True,False]
```

The colon operator (":") is a shorthand for a cons. A cons prepends the element on the left side of the colon operator to the list on the right side. The concatenation operator ("++") can be used to add an element to the end of a list. The empty list `[]` can be consed with anything.
```
> let numbers = [1, 2]

> 0:numbers
[0,1,2]

> (-2):(-1):numbers
[-2,-1,1,2]

> numbers ++ [3]
[1,2,3]
```

The ".." range operator is a shortcut for constructing a list based on a range of values. The range operator takes in an optional step parameter. The range operator is useful when combined with lazy evaluation.
```
> [0,2..10]
[0,2,4,6,8,10]

> ['A'..'z']
"ABCDEFGHIJKLMNOPQRSTUVWXYZ[\\]^_`abcdefghijklmnopqrstuvwxyz"

> take 10 [13,26..]
[13,26,39,52,65,78,91,104,117,130]
```

Lists can be nested.
```
> [[1, 2], [3, 4]]
[[1,2],[3,4]]
```

The "++" operator concatenates two lists. The "!!" operator is used to index into a list.
```
> [1, 2] ++ [3, 4]
[1,2,3,4

> "hello" !! 3
'l'
```

A list comprehension applies some function to a list to create a new list. Comprehensions can include multiple optional predicates. The input to a list comprehension can consist of multiple lists.
```
> [x * 2 | x <- [1..10]]
[2,4,6,8,10,12,14,16,18,20]

> [x ^ 2 | x <- [0..5], x/= 1, x /= 3]
[0,4,16,25]

> [x * 2 | x <- [1..10], x * 2 > 10]
[12,14,16,18,20]

> [x | x <- [0..100], x `mod` 20 == 0]
[0,20,40,60,80,100]

> [if x `mod` 3 == 0 then "div3" else "-" | x <- [0..5]]
["div3","-","-","div3","-","-"]

> [x * y | x <- [1, 2], y <- [3, 4]]
[3,4,6,8]
```



LIST FUNCTIONS
--------------

The `map` function performs some operation on each element in a list.
```
> :t map
map :: (a -> b) -> [a] -> [b]

> map (+1) [1, 2, 3]
[2,3,4]

> map not [True, False, True]
[False,True,False]

> map toUpper "abc"
"ABC"
```

The `fmap` function is similar to `map`, but for the `Functor` data type. When applied to a `Maybe` data type, it only performs the operation if the value is not `Nothing`. It takes in a function and a list that can be mapped over and produces the result of applying the function to each element in the list.
```
> :t fmap
fmap :: Functor f => (a -> b) -> f a -> f b

> fmap (2*) [1,2,3,4]
[2,4,6,8]

> fmap (fmap (2*)) [Just 1, Just 2, Nothing]
[Just 2,Just 4,Nothing]
```

The `filter` function filters the elements of a list based on a predicate.
```
> filter (>1) [0, 1, 2, 3]
[2,3]

> filter (even) [0, 1, 2, 3]
[0,2]
```

The `fold` function takes in a function and a list, and combines all of the elements of the list into a result value. The `foldr` function is right-associative--it folds a list from right to left. The first argument to `foldr` is a function that takes two arguments, and the second argument is the initial value for the accumulator. The `foldl` function is left-associative. The `foldl'` function is a version of `foldl` that forces its function argument to be evaluated immediately, which makes it more efficient. Prefer using `foldl'` for most cases.
```
> :t foldl
foldl :: Foldable t => (b -> a -> b) -> b -> t a -> b

> foldl (-) 6 [3, 2, 1]
0

> and' l = foldr (&&) True l
> and' [True,False]
False
```

The `foldr1` and `foldl1` functions automatically initialize the accumulator to the last element of the list. With `foldr1` and `foldl1`, all types have to be the same, and an empty list generates an error.
```
> maximum' l = foldr1 (max) l
> maximum' [1,2,4,3]
4
```


The `scan` family of functions combine the functionality of `map` and `fold`, by returning the list of intermediate values that were accumulated by applying some function. The `scanl` function accumulates a list from left to right. The `scanl1` function automatically uses the first item in the list as the initial value. The `scanr` and `scanr1` functions are the right-to-left counterparts of the `scanl` and `scanl1` functions.
```
> :t scanl
scanl :: (b -> a -> b) -> b -> [a] -> [b]
> :t scanl1
scanl1 :: (a -> a -> a) -> [a] -> [a]

> scanr (+) 0 [1,2,3]
[6,5,3,0]

> scanr1 (+) [1,2,3]
[6,5,3]
```

The `filter` function filters a list of elements based on a condition function.
```
> :t filter
filter :: (a -> Bool) -> [a] -> [a]

> filter (< 5) [1, 10, 100]
[1]
```

The `head` function returns the first element of a list. The `tail` function returns everything but the head of a list. The `last` function returns the last element of a list. The `init` function returns everything but the last element of a list.
```
> head [1, 2, 3]
1

> tail [1, 2, 3]
[2,3]

> last [1, 2, 3]
3

> init [1, 2, 3]
[1,2]
```

The `length` function returns the length of a list. The `null` function checks if a list is empty. The `reverse` function reverses the elements in a list.
```
> length [1, 2, 3]
3

> null []
True

> reverse [1, 2, 3]
[3,2,1]
```

The `take` function extracts a specified number of elements from the beginning of a list. The `drop` drops a specified number of elements from the beginning of a list, and returns the rest.
```
> take 2 [1, 2, 3]
[1,2]

> drop 2 [1, 2, 3]
[3]
```

The `maximum` function returns the biggest element in the list. The `minimum` function returns the smallest element in a list. The `sum` function returns the sum of a list of numbers. The `product` function returns the product of a list of numbers.
```
> maximum [1, 2, 3]
3

> minimum [1, 2, 3]
1

> sum [1, 2, 3]
6

> product [1, 2, 3]
6
```

The `elem` function returns a Boolean value indicating whether a value is an element of the list.
```
> elem 4 [1, 2, 3]
False
```

The `cycle` function creates an infinite list by iterating over its argument. The `repeat` function is similar, except that it works on a single element. The `replicate` function constructs a new list by repeating its argument a specified number of times.
```
> take 6 (cycle [1, 2])
[1,2,1,2,1,2]

> take 3 (repeat 1)
[1,1,1]

> replicate 3 1
[1,1,1]
```



TUPLES
------

Tuples consist of at least two comma-separated elements inside parentheses ("()"). Tuples have a fixed size and can contain values of different types. Tuples are useful for fixed size data and when you want to return multiple values from a function.
```
> (True, 1)
(True,1)
> (3.0, 'a', "hello", 5)
(3.0,'a',"hello",5)
```

Tuples can be stored inside other tuples.
```
> ((2,3), True)
((2,3),True)

> ((2,3), [2,3])
((2,3),[2,3])

> [(1,2), (3,4), (5,6)]
[(1,2),(3,4),(5,6)]
```

The `fst` function returns the first element of a pair. The `snd` function returns the second element of a pair. The `zip` function takes in two list and creates a list of pairs by joining each element in the first list to its corresponding element in the second list.
```
> fst (1, 2)
1

> snd (1, 2)
2

> zip [0..5] ['a'..'z']
[(0,'a'),(1,'b'),(2,'c'),(3,'d'),(4,'e'),(5,'f')]
```



OPERATORS
---------

Operators acting on two operands that consist only of non-alphanumeric characters can be written as infix or as prefix surrounded by parenthesis.
```
> 4 + 9 == 13
True

> (==) ((+) 4 9) 13
True
```

Arithmetic operators: +, -, *, `div` (integer division), / (floating-point division), ^ (integer exponentiation), ^^ (fractional exponentiation), ** (floating-point exponentiation), `mod` (modulus). Negated numbers should be placed inside parentheses to avoid them being parsed as a subtraction.
```
> 3 * (5 + 2)
21

> 5 ** (-2)
4.0e-2

> 17 `mod` 10
7
```

Boolean operators: &&, ||, not.
```
> True && True || False
True

> not (1 < 2)
False
```

Relational operators: ==, /=, <, >, <=, >=.
```
> 2 + 3 == 5
True

> "hello" == "hello"
True

> True /= True
False

> [3, 2, 1] > [3, 2, 0]
True
```

List operators: `!!` (indexing), `++` (concatenation), `:` (cons), `elem` (list membership), `notElem` (list non-membership).
```
> "hello" ++ " " ++ "world" 
"hello world"

> [1,2,3] !! 2
3
```

Function operators: `.` (function composition), `$` (infix function application).

Monadic operators: `>>` (bind ignoring return value), `>>=` (bind piping the return value to the next function), `=<<` (reverse monadic bind).



VARIABLES
---------

Haskell variables are immutable and can only be declared once. Variables can be defined in any order.
```
r = 5
area = pi * r ^ 2
```



FUNCTIONS
---------

Function definitions are similar to variable definitions, except that they take some number of arguments. Arguments in a function call are separated by spaces.
```
areaRect x y = x * y

areaCircle r = pi * r ^ 2
areaCircle 5

volumeCylinder h r = h * areaCircle r
```

Functions have a higher precedence than other operators. Functions can be written as infix operators when surrounded by backticks ("`").
```
> 25 `div` 9
2

> 1 `elem` [1..4]
True
```

The `where` clause defines an intermediate result local to a function. Placing a `where` clause at the same indentation level as the conditional cases makes it available for all cases.
```
heron a b c = sqrt (s * (s - a) * (s - b) * (s - c))
  where s = (a + b + c) / 2

doStuff :: Int -> String
doStuff x
  | x < 3     = report "less than three"
  | otherwise = report "normal"
  where
    report y = "the input is " ++ y
```

The `let` clause is an alternative to `where`. A `let` clause introduces one or more local bindings to the expression following the "in" keyword. While `where` introduces the binding after the expression, `let` introduces its binding before the associated expression.
```
roots a b c =
  let sdisc = sqrt (b * b - 4 * a * c)
    twiceA = 2 * a
  in  ((-b + sdisc) / twiceA,
    (-b - sdisc) / twiceA)

f x =
  if x > 0
    then (let lsq = (log x) ^ 2 in tan lsq) * sin x
    else 0
```

The dollar sign operator ("$") performs a simple function application, but with low precedence. For example, `f g $ h x` would be equivalent to `(f g) (h x)`; `f $ g h x` is equivalent to `f ((g h) x)`.
```
> :t ($)
($) :: (a -> b) -> a -> b

> (+3) $ 5
8
```

The `(.)` function composition operator is a short-hand for writing function compositions with high precedence. If `f` and `g` are functions, then `f . g` is the function which does first `g` and then `f`. Calling `(f . g) x` is equivalent to `f (g x)`; calling `(f . g . h) x` is equivalent to `f (g (h x))`. By chaining the functions, it is possible to define a function without referencing its arguments ("point-free" style).
```
> :t (.)
(.) :: (b -> c) -> (a -> b) -> a -> c

> bar = sqrt . (+ 5)
> bar 20
5.0

> foo = sum . map (\x -> x * x) . filter (>3)
> foo [2, 4, 6]
52
```

The backslash operator ("\") creates an unnamed lambda function. The variables introduced inside a lambda expression are local to that expression. A section is syntactic sugar for a lambda operation. For example, the section `(+2)` is equivalent to `\x -> x + 2`.
```
map f = foldr (\x xs -> f x : xs) []

tail' = (\(_:xs) -> xs)

> map (4+) [1,2,3]
[5,6,7]
> map (\x -> x + 4) [1,2,3]
[5,6,7]

> (1 `elem`) [1,2,3]
True
```

Recursion can be written in several different ways. Pattern matching is an easy way to define the base case for a recursive definition. It is important to list the base case first in order to avoid the compiler from getting stuck in an infinite loop.
```
factorial 0 = 1
factorial n = n * factorial (n - 1)

factorial n = go n 1
    where
    go n res
        | n > 1     = go (n - 1) (res * n)
        | otherwise = res

length' :: [a] -> Int
length' []     = 0
length' (x:xs) = 1 + length xs

(++) :: [a] -> [a] -> [a]
[] ++ ys     = ys
(x:xs) ++ ys = x : xs ++ ys

zip' :: [a] -> [b] -> [(a, b)]
zip' a []    = []
zip' [] b    = []
zip' a b     = (head a, head b) : zip' (tail a) (tail b)
```



HIGHER ORDER FUNCTONS
---------------------

The `uncurry` function converts a function of two arguments into a function that takes a pair as its argument. The `curry` function is the opposite of `uncurry`, converting a function that takes in a pair to a function that takes in two arguments.
```
> :t uncurry
uncurry :: (a -> b -> c) -> (a, b) -> c
> :t curry
curry :: ((a, b) -> c) -> a -> b -> c

> uncurry (+) (3, 5)
8
> curry (uncurry (+)) 3 5
8
```

The `id` function, is the identity function--it returns its argument unchanged. The `const` function takes two arguments, and returns a constant function that always returns the first argument.
```
> :t id
id :: a -> a
> :t const
const :: a -> b -> a

> id "Hello"
"Hello"
> const 3 5
3
```



CONDITIONALS
------------

An "if" statement is an expression that has to return a value. This means that the "then" and "else" clauses of a Haskell if-statement are mandatory. All of the expressions inside an if-statement must be evaluate to the same type.
```
absolute x =
    if x < 0
        then -x
        else x

mySignum x =
    if x < 0
        then -1
        else if x > 0
            then 1
            else 0

g x y = (if x == 0 then 1 else sin x / x) * y
```

Guards act like an if-statement, using the result of boolean expressions to change the control flow of the program. The `otherwise` expression is evaluated if none of the preceding conditions evaluate to `True`.
```
mySignum x
    | x < 0     = -1
    | x > 0     = 1
    | otherwise = 0
```

A `case` expression matches a value against one of a number of options. It returns the value of the first matching pattern.
```
f x = case x of
    0 -> 18
    1 -> 15
    2 -> 12
    _ -> 12 - x
```

The `compare` function takes in two `Ord` values and returns a value of type `Ordering` (`LT`, `GT`, or `EQ`).
```
checkGuess guess num = case compare guess num of
    LT -> -1 
    GT -> 1
    EQ -> 0
```



PATTERN MATCHING
----------------

Pattern matching involves matching an argument against one or more patterns. Pattern matching allows you to recognize values, bind variables, and decompose values into parts. Pattern matching can be used in any place where variables are bound: functions, `let` expressions, `where` clauses, `do` blocks, lambda abstractions, and after "|" in list comprehensions.

The "_" character stands for the wildcard pattern, which matches anything anything without a binding. The "[]" pattern matches the empty list. The "(x:xs)" pattern matches a non-empty list, where "x" is a single element cons'ed to the list "xs".
```
map _ []     = []
map f (x:xs) = f x : map f xs

f :: Int -> Int
f 0 = 1
f 1 = 5
f 2 = 2
f _ = -1

(||) :: Bool -> Bool -> Bool
False || False = False
_     || _     = True

putFirstChar = do
  (c:_) <- getLine
  putStrLn [c]

fst' :: (a, b) -> a
fst' (x, _) = x

head' :: [a] -> a
head' (x:_) =  x
head' []    =  error "Prelude.head: empty list"

dropThree :: [a] -> [a]
dropThree (_:_:_:xs) = xs
dropThree _
```

The "@" character can be used to bind a name to a matched value, by using the form `var@pattern`.
```
contrivedMap f [] = []
contrivedMap f list@(x:xs) = f list x : contrivedMap f xs
```

Records allow you to name values in a datatype, using the syntax `{name::value}`.
```
data Foo2 = Bar2 | Baz2 {bazNumber::Int, bazName::String}
```



EXCEPTIONS / ERRORS
-------------------

Partial functions are functions that don't handle all possible inputs. For example, `head`, `tail`, `last`, and `init` all fail when the input is an empty list. The `!!` functions fail when you supply an index that is too large. Best practice is to avoid the use of partial functions. Instead, use pattern matching that handles all possible input combinations. If you have to use a partial function, then use it with the `Maybe` [monad](@0017) or use the safer alternatives provided in the `safe` package (e.g. `safeHead`).
```
> head []
*** Exception: Prelude.head: empty list

> [] !! 2
*** Exception: Prelude.!!: index too large
```



MONADS
------

Haskell uses the `Monad` type class to implement monads. The monad of type `M a` is interpreted as a statement in an imperative language `M` that returns a value of type `a`. Every monad is also a `Functor` and an `Applicative`, which means that the `fmap`, `pure`, and `(<*>)` functions can be used on them. The `(<*>)` operator performs sequential application, similar to the "do" expression.

A monad is defined by a type constructor `m`, a function `return`, and a bind operator `(>>=)`. The "then" `(>>)` operator (alternative to the "do" keyword) sequences two independent monadic actions (similar to the semicolon (";") in imperative programming languages). Any amount of actions can be chained as long as they are in the same monad. The bind operator `>>=` combines two computational steps, by passing the value from the previous operation downstream the binding sequence. This allows you to pass non-monadic values to a function without leaving a monad. Bind is like a semicolon followed by an assignment from the previous operation.
```
class Applicative m => Monad m where
  return :: a -> m a
  (>>=)  :: m a -> (a -> m b) -> m b

  (>>)   :: m a -> m b -> m b
  fail   :: String -> m a
```

There are many different types of monads, each of which performing some specific function. The following are commonly used monads in Haskell.

`[]`
: Non-determinism.

`Error`
: Exception (with error description).

`IO`
: Input/Output.

`Maybe`
: Exception (anonymous).

`Reader`
: Environment.

`State`
: Global state.

`Writer`
: Logger.


The `Maybe` monad represents computations that might go wrong.
```
return :: a -> Maybe a
return x = Just x

(>>=) :: Maybe a -> (a -> Maybe b) -> Maybe b
(>>=) m g = case m of
    Nothing -> Nothing
    Just x  -> g x
```

`Maybe` can be used to create safe implementations of computations with undefined results (list operations on empty lists, lookups with no results, math operations such as `sqrt` on negative numbers). To combine uncertain computations, use the bind operator (">>=") or the backwards arrow ("<-") in a do-block. The `Maybe` monad propagates failures. If any of the computations return `Nothing`, then the whole block returns `Nothing`.
```
safeLog :: (Floating a, Ord a) => a -> Maybe a
safeLog x
    | x > 0     = Just (log x)
    | otherwise = Nothing

lookup :: Eq a => a
    -> [(a, b)]
    -> Maybe b 

getTaxOwed :: String
getTaxOwed name =
  lookup name phonebook >>=
    (\number -> lookup number governmentDatabase) >>=
      (\registration -> lookup registration taxDatabase)

getTaxOwed name = do
  number       <- lookup name phonebook
  registration <- lookup number governmentDatabase
  lookup registration taxDatabase
```

The `maybe` function takes in a default value, a computation and an argument, and produces the output depending on whether the computation returned `Nothing`. The `fromMaybe` function in `Data.Maybe` replaces `Nothing` in a `Maybe` computation with some default value.
```
zeroAsDefault :: Maybe Int -> Int
zeroAsDefault mx = fromMaybe 0 mx

displayResult :: Maybe Int -> String
displayResult mx = maybe "There was no result" (("The result was " ++) . show) mx

> :t maybe
maybe :: b -> (a -> b) -> Maybe a -> b

> :m Data.Maybe
> :t fromMaybe
fromMaybe :: a -> Maybe a -> a

> displayResult (Just 10)
"The result was 10"
> displayResult Nothing
"There was no result"
```

The list monad (`[]`) models non-deterministic computations. The `return` function for the list monad injects a value into a list. The binding operator pulls out the values from a list and passes them to a function to create a new list. The list binding operator uses a combination of concatenation and mapping.
```
return :: a -> [] a
return x = [x]

(>>=) :: [a] -> (a -> [b]) -> [b]
xs >>= f = concat (map f xs)

fs <*> xs = concatMap (\f -> map f xs) fs

> concat (map (\x -> (replicate x x)) [1..3])
[1,2,2,3,3,3]
> [(*3), (+5)] <*> [1,2,3]
[3,6,9,6,7,8]
```

The `IO` monad allows Haskell to represent actions as values. `IO` actions are said to be "impure", because they introduce side-effects. Functions that have the `IO ()` type do not pass on values to later functions. The IO monad is most commonly used with the "do" operator, which is almost equivalent to the `(>>)` (then) operator.
```
x <- readLn
print x

readLn >>= \x -> print x
```

The `State` monad describes functions that consume a state and produce a result and an updated state, given back as a tuple.
```
state :: (s -> (a, s)) -> State s a
```



LIBRARIES AND MODULES
---------------------

Modules are the main way Haskell code is organized. Module names must begin with a capital letter. 

Prelude is the core library loaded into every Haskell program. If you want to include other libraries, you have to be import them as modules in your program. Modules are imported using the "import" keyword. Use parenthesis to only select the functions you choose, rather than the whole library.
```
import Data.List
import Data.Char (toLower, toUpper)
testPermutations = permutations "Prelude"
```

The "qualified" keyword is useful when you dealing with conflicting namespaces across libraries. When used, the "qualified" keyword forces you to specify the entire function name path. The "as" keyword allows you to rename an imported module name.
```
import qualified MyModule
import qualified VeryLongModuleName as MyModule1

MyModule.funcName x
MyModule1.funcName x
```

The "hiding" keyword can be used to exclude specific functions from an export. Algebraic data types and type synonyms can not be hidden.
```
import Data.Char hiding (toLower, toUpper)
```

In the GHCi interactive prompt, you can import modules using the `:m` or `import` command.
```
Prelude> :m +Data.List
Prelude Data.List> :t permutations
permutations :: [a] -> [[a]]

Prelude> import Data.List
Prelude Data.List>
```

A module definition has the form: `module ModuleName where`. A module with the name `ModuleName` should be in the file "ModuleName.hs", while a module with the name `Module.Sub` should be in the file "Module/Sub.hs" (in the subdirectory).

When writing your own module, you can select which functions are meant to be exported by putting them inside parenthesis.
```
module MyModule (exported1, exported2) where
exported1 x = x + 1
exported2 x = x + 2
notExported1 = x + 3
```



[^esposito]: Esposito, Yann (2020, May 26) Learn Haskell Fast and Hard. yannesposito.[^esposito]

[^lipovaca]: Lipovaca, M. (2011). Learn you a haskell for great good!: a beginner's guide. no starch press.[^lipovaca]

[^upenn]: Yorgey, C. (2020, May 28).  Introduction to Haskell (Spring 2013). University of Pennsylvania. Retrieved from https://www.seas.upenn.edu/~cis194/spring13/.[^upenn]

[^wikibooks_haskell]: Haskell/Lists and tuples. (2020, April 16). Wikibooks, The Free Textbook Project.[^wikibooks_haskell]

[^wikibooks_scm]: Write Yourself a Scheme in 48 Hours/First Steps. (2020, April 16). Wikibooks, The Free Textbook Project.[^wikibooks_scm]
